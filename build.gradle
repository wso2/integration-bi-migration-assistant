plugins {
    id 'java'
    id 'application'
    id 'checkstyle'
    id 'com.github.spotbugs' version '5.1.3'
}

group 'com.wso2'
version '0.1.0-SNAPSHOT'

repositories {
    mavenCentral()
    maven {
        url = 'https://maven.wso2.org/nexus/content/repositories/orgballerinalang-1614'
    }
    maven {
        url = 'https://maven.pkg.github.com/ballerina-platform/*'
        credentials {
            username System.getenv("packageUser")
            password System.getenv("packagePAT")
        }
    }
}

dependencies {
    implementation group: 'org.ballerinalang', name: 'ballerina-parser', version: "${ballerinaLangVersion}"
    implementation group: 'org.ballerinalang', name: 'formatter-core', version: "${ballerinaLangVersion}"
    implementation group: 'org.ballerinalang', name: 'ballerina-tools-api', version: "${ballerinaLangVersion}"
    implementation group: 'org.antlr', name: 'antlr4', version: "${antlrVersion}"
    testImplementation 'org.testng:testng:7.7.0'
    spotbugs 'com.github.spotbugs:spotbugs:4.8.4'
}

checkstyle {
    toolVersion = '10.3.4'
    config = resources.text.fromUri('https://raw.githubusercontent.com/wso2/code-quality-tools/v1.4/checkstyle/jdk-17/checkstyle.xml')
}

spotbugs {
    reportLevel = 'high'
    excludeFilter = file('spotbugs-exclude.xml')
}

tasks.withType(com.github.spotbugs.snom.SpotBugsTask).configureEach {
    reports {
        html.required = true
        xml.required = false
    }
}

test {
    useTestNG() {
        suites 'src/test/resources/testng.xml'
    }
}

application {
    mainClassName = 'converter.MigrationTool'
}

jar {
    manifest {
        attributes(
                'Main-Class': 'converter.MigrationTool'
        )
    }
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

build {
    dependsOn checkstyleMain
    dependsOn spotbugsMain
}

// Apply a specific Java toolchain to ease working on different environments.
java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}
