plugins {
    id 'java'
    id 'application'
    id 'checkstyle'
    id 'com.github.spotbugs' version '5.1.3'
}

group 'com.wso2'
version '0.1.0-SNAPSHOT'

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        url = 'https://maven.wso2.org/nexus/content/repositories/orgballerinalang-1614'
    }
    maven {
        url = 'https://maven.pkg.github.com/ballerina-platform/*'
        credentials {
            username System.getenv("packageUser")
            password System.getenv("packagePAT")
        }
    }
}

dependencies {
    // tibco
    implementation project(path: ':tibco')

    // Dev dependencies
    implementation group: 'org.ballerinalang', name: 'ballerina-cli', version: "${ballerinaLangVersion}"
    implementation group: 'info.picocli', name: 'picocli', version: "${picocliVersion}"
    implementation group: 'org.jetbrains', name: 'annotations', version: "${jetbrainsAnnotationsVersion}"
    testImplementation group: 'org.testng', name: 'testng', version: "${testngVersion}"
    spotbugs group: 'com.github.spotbugs', name: 'spotbugs', version: "${spotbugsVersion}"
}

checkstyle {
    toolVersion = '10.12.5'
    config = resources.text.fromUri('https://raw.githubusercontent.com/wso2/code-quality-tools/v1.4/checkstyle/jdk-17/checkstyle.xml')
}

spotbugs {
    reportLevel = 'high'
    excludeFilter = file("${rootProject.projectDir}/spotbugs-exclude.xml")
}

tasks.withType(com.github.spotbugs.snom.SpotBugsTask).configureEach {
    reports {
        html.required = true
        xml.required = false
    }
}

test {
    useTestNG() {
        suites 'src/test/resources/testng.xml'
    }
}

// Keep this for backward compatibility
application {
    mainClassName = 'cli.Main'
}

// Default jar task - we'll keep it for backward compatibility
jar {
    manifest {
        attributes(
                'Main-Class': 'cli.Main'
        )
    }
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    archiveFileName = 'integration-bi-migration-assistant.jar'
}

// Create a separate JAR for Tibco migrations
task tibcoJar(type: Jar) {
    manifest {
        attributes(
                'Main-Class': 'cli.TibcoCli'
        )
    }
    from {
        sourceSets.main.output
    }
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    archiveBaseName = 'tibco-migration-assistant'
    archiveVersion = project.version
}

task tibcoPack(type: Exec, dependsOn: tibcoJar) {
    workingDir "${projectDir}/src/main/java/baltool/tibco/tool-bi-migrate-tibco"
    commandLine 'bal', 'pack'
    group = 'build'
}

task tibcoRelease(type: Exec, dependsOn: tibcoPack) {
    workingDir "${projectDir}/src/main/java/baltool/tibco/tool-bi-migrate-tibco"
    commandLine 'bal', 'push'
    group = 'build'
}

build {
    dependsOn ":tibco:build"
    dependsOn checkstyleMain
    dependsOn spotbugsMain
    dependsOn tibcoJar
}

// Apply a specific Java toolchain to ease working on different environments.
java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}
