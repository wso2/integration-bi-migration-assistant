plugins {
    id 'java'
    id 'application'
    id 'checkstyle'
    id 'com.github.spotbugs' version '5.1.3'
}

group 'com.wso2'
version '0.1.0-SNAPSHOT'

dependencies {
    // ballerina-lang
    implementation group: 'org.ballerinalang', name: 'ballerina-parser', version: "${ballerinaLangVersion}"
    implementation group: 'org.ballerinalang', name: 'formatter-core', version: "${ballerinaLangVersion}"
    implementation group: 'org.ballerinalang', name: 'ballerina-tools-api', version: "${ballerinaLangVersion}"

    // misc
    implementation group: 'org.antlr', name: 'antlr4', version: "${antlrVersion}"
    implementation group: 'info.picocli', name: 'picocli', version: "${picocliVersion}"
    implementation group: 'org.yaml', name: 'snakeyaml', version: "${snakeYamlVersion}"

    // Jackson dependencies
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: "${fasterXmlVersion}"
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-annotations', version: "${fasterXmlVersion}"
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: "${fasterXmlVersion}"
    implementation group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jdk8', version: "${fasterXmlVersion}"

    // common
    implementation project(path: ':common')

    // Dev dependencies
    implementation group: 'org.jetbrains', name: 'annotations', version: "${jetbrainsAnnotationsVersion}"
    testImplementation group: 'org.testng', name: 'testng', version: "${testngVersion}"
    spotbugs group: 'com.github.spotbugs', name: 'spotbugs', version: "${spotbugsVersion}"
}

checkstyle {
    toolVersion = '10.12.5'
    config = resources.text.fromUri('https://raw.githubusercontent.com/wso2/code-quality-tools/v1.4/checkstyle/jdk-17/checkstyle.xml')
}

checkstyleMain.exclude("mule/v3/dataweave/parser/*.java")
checkstyleMain.exclude("mule/v4/dataweave/parser/*.java")

// To bypass 120 word limit for html template
checkstyleMain.exclude("mule/v3/report/MigrationReportTemplate.java")
checkstyleMain.exclude("mule/v3/report/AggregateReportTemplate.java")
checkstyleMain.exclude("mule/v3/report/AggregateReportWriter.java")
checkstyleMain.exclude("mule/v4/report/MigrationReportTemplate.java")
checkstyleMain.exclude("mule/v4/report/AggregateReportTemplate.java")
checkstyleMain.exclude("mule/v4/report/AggregateReportWriter.java")

spotbugs {
    reportLevel = 'high'
    excludeFilter = file("${rootProject.projectDir}/spotbugs-exclude.xml")
}

tasks.withType(com.github.spotbugs.snom.SpotBugsTask).configureEach {
    reports {
        html.required = true
        xml.required = false
    }
}

// ANTLR code generation tasks
task generateDataWeaveV3Parser(type: JavaExec) {
    group = 'antlr'
    description = 'Generate ANTLR parser classes for DataWeave v3 grammar'

    classpath = configurations.runtimeClasspath
    mainClass = 'org.antlr.v4.Tool'

    args = [
        '-package', 'mule.v3.dataweave.parser',
        '-o', '.',
        '-visitor',
        'src/main/java/mule/v3/dataweave/parser/DataWeave.g4'
    ]
}

task generateDataWeaveV4Parser(type: JavaExec) {
    group = 'antlr'
    description = 'Generate ANTLR parser classes for DataWeave v4 grammar'

    classpath = configurations.runtimeClasspath
    mainClass = 'org.antlr.v4.Tool'

    args = [
        '-package', 'mule.v4.dataweave.parser',
        '-o', '.',
        '-visitor',
        'src/main/java/mule/v4/dataweave/parser/DataWeave.g4'
    ]
}

test {
    useTestNG() {
        suites 'src/test/resources/testng.xml'
    }
}

build {
    dependsOn ":common:build"
    dependsOn checkstyleMain
    dependsOn spotbugsMain
}
